UDESC - Universidade do Estado de Santa Catarina		Data: 04/09/2020
Alunos: Menderson Costa e Paulo Albuquerque
Disciplina: Compiladores
Trabalho 1 - Etapa 1 â€“ Gramatica na Forma de Backus Naur

<algoritmo>        	::= <var_global> <funcao-main> <outras_funcoes>
                    | <var_global> <funcao-main>
                    | <funcao-main>
                    | <funcao-main> <outras_funcoes>

<var_global>		::= <var_global> <var_global>
					| <init_vars>

<outras_funcoes>	::= <outras_funcoes> <outras_funcoes>
					| <funcao>

<funcao>	        ::= <tipo_var> <nome_func> "(" <argumentos> ")" "{" <codigo> "}"

<argumentos>		::= <tipo_var> <valor>
					| <tipo_var> <valor> "," <argumentos>

<funcao_main>		::= <tipo_var> "main" "(" ")" "{" <codigo> "}" 

<codigo>			::= <codigo> <codigo>
					| <linha>

<linha>				::= <variaveis>
					| <lacos>
					| <io>
					| <condicional>

<condicional>		::= <ifelse>
					| <if>

<if>				::= "if" "(" <condicao> ")" "{" <codigo> "}" 
					| "if" "(" <condicao> ")" <linha> 

<ifelse> 			::= "if" "(" <condicao> ")" "{" <codigo> "}" "else" "{" <codigo> "}" 
					| "if" "(" <condicao> ")" <linha> "else" "{"<linha> "}"
					| "if" "(" <condicao> ")" <linha> "else" "{"<codigo> "}"
					| "if" "(" <condicao> ")" "{" <codigo> "}" "else" <linha>

<io>				::= <read>
					| <print>

<read>				::= "read" <nome_var> ";"

<print>				::= "print" <print2>

<print2>			::= <print2> <print2>
					| <texto>
					| <nome_var>

<lacos> 			::= <do>
					| <while>
					| <for>

<do>				::= "do:" "{" <codigo> "}" "while" "(" <condicao> ")" ";"

<while>				::= "while" "(" <condicao> "):" "{" <codigo> "}" ";" 
                    | "while" "(" <condicao> "):" "{" <linha> "}" ";"

<for>				::= "for" "(" <nome_var> ":" <valor> "," <valor> "," <valor> "):" "{" <codigo> "}" ";" 
                    | "for" "(" <nome_var> ":" <valor> "," <valor> "," <valor> "):" <linha> ";"

<condicao>			::= <condicao> "or" <condicao>
					| <condicao> "and" <condicao>
					| <expr_cond>

<expr_cond>			::= <expressao> <op_logico> <expressao>

<op_logico>			::= "<" | "<=" | ">" | ">=" | "==" | "!="

<variaveis> 		::= <init_vars>
					| <atribuir_var>

<atribuir_var>		::= <espaco> <nome_var> "=" <expressao> ";"

<init_vars>			::= <tipo_var> <espaco> <nome_var> "=" <expressao> ";"
					| <tipo_var> <espaco> <nome_var> ";"

<expressao> 		::= <term> "+" <expressao>
					| <term> "-" <expressao>
					| <term>

<term> 				::= <factor> "*" <term>
					| <factor> "/" <term>
					| <factor>

<factor>			::= <expo> "^" <factor>
					| <expo>

<expo>				::= "(" <expressao> ")"
					|  <valor>
					| <chamada_func>

<chamada_func>		::= <nome_func> "(" <parametros> ")"
					| <nome_func> "(" ")"

<parametros>		::= <valor>
					| <valor> "," <parametros>

<tipo_var>         	::= "bool"
					| "int"
					| "float"

<nome_var>          ::= <minuscula> <cadeia_caracter>

<nome_func>			::= <minuscula> <cadeia_caracter>

<valor>             ::= <valor_inteiro>
					| <valor_float>

<valor_inteiro>     ::= <numeros> <valor_inteiro>
					| <numeros>

<valor_float>       ::= <valor_inteiro> "." <valor_inteiro>

<cadeia_caracter>	::= <minuscula> <cadeia_caracter>
					| <maiuscula> <cadeia_caracter>
					| <numeros> <cadeia_caracter>
					| ""

<texto>				::= <minuscula> <cadeia_caracter>
					| <maiuscula> <cadeia_caracter>
					| <numeros> <cadeia_caracter>
					| <carac_especial> <cadeia_caracter>
					| ""

<carac_especial> 	::= " " | "!" | "#" | "$" | "@" | "%" | "&" | "^" | "*" | "(" | ")" 
                    | "+" | "-" | "/" | "=" | "," | "." | ":" | ";" | "?" | "<" | ">" 
                    | "[" | "]" | "{" | "}" | "|" | "~" | "\n"

<minuscula>		    ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" 
                    | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y"| "z"

<maiuscula>	 	    ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
					| "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

<numeros>		    ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "0"